app MakeLoop {
  wasp: {
    version: "^0.13.0"
  },
  title: "MakeLoop",
  head: [
    "<link rel=\"icon\" href=\"/favicon.ico\" type=\"image/x-icon\" />"
  ],
  client: {
    rootComponent: import { Layout } from "@src/Layout.jsx",
  },
  db: {
    system: PostgreSQL,
    prisma: {
      clientPreviewFeatures: ["extendedWhereUnique"]
    },
    seeds: [
      import { resetAll } from "@src/scripts.js",
      import { devSeedUsers } from "@src/scripts.js",
    ]
  },
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {
        userSignupFields: import { userSignupFields } from "@src/pages/auth/signup.js",
      }
    },
    onAuthFailedRedirectTo: "/",
    onAuthSucceededRedirectTo: "/"
  }
}

// Routes
route HomeRoute { path: "/", to: HomePage }
page HomePage {
  component: import HomePage from "@src/pages/views/HomePage.jsx",
  authRequired: false
}
route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/pages/auth/Login.jsx"
}
route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@src/pages/auth/Signup.jsx"
}
// route ProfileRoute { path: "/:username", to: ProfilePage }
// page ProfilePage {
//   component: import ProfilePage from "@src/pages/views/Profile.jsx"
// }
route NotFound { path: "/pages/404", to: NotFoundPage }
page NotFoundPage {
  component: import NotFoundPage from "@src/pages/views/NotFound.jsx",
  authRequired: false
}

// Entities
entity User {=psl
  id        Int     @id @default(autoincrement())
  username  String  @unique
  loops     Loop[]  @relation("UserLoops")
psl=}

entity Loop {=psl
  id                        Int        @id @default(autoincrement())
  name                      String
  description               String?
  isActive                  Boolean    @default(true)
  userId                    Int
  user                      User       @relation("UserLoops", fields: [userId], references: [id])
  iterations                Iteration[]
psl=}

entity Iteration {=psl
  id         Int      @id @default(autoincrement())
  loopId     Int
  loop       Loop     @relation(fields: [loopId], references: [id])
  startTime  DateTime
  endTime    DateTime
  checkin    String
  isComplete Boolean  @default(false)
psl=}

// Actions
action createLoop {
  fn: import { createLoop } from "@src/actions.js",
  entities: [User, Loop]
}

action addCheckin {
  fn: import { addCheckin } from "@src/actions.js",
  entities: [User, Loop]
}

// Queries
query getLoops {
  fn: import { getLoops } from "@src/queries.js",
  entities: [Loop]
}

query getActiveLoops {
  fn: import { getActiveLoops } from "@src/queries.js",
  entities: [Loop, User]
}
query getUsernameFromLoopID {
  fn: import { getUsernameFromLoopID } from "@src/queries.js",
  entities: [User]
}

query getIterations {
  fn: import { getIterations } from "@src/queries.js",
  entities: [Iteration]
}